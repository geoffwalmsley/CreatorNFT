(mod (MOD_HASH STATE ROYALTY Truths new_state payment_info)



     ;; STATE = (for_sale_flag, price, owner_ph, owner_pk)
     ;; ROYALTY = (creator_puzhash, percentage)

     ;; new_state: update for new STATE
     ;; payment_info: p2_singleton_coin_id
     ;; fee: payment fee in mojo



     (include condition_codes.clib)
     (include sha256tree.clib)
     (include singleton_truths.clib)
     (include curry_and_treehash.clib)

     (defun new_puzzle_hash (MOD_HASH ROYALTY new_state)
         (puzzle-hash-of-curried-function MOD_HASH
	 				  (sha256tree ROYALTY)
	 				  (sha256tree new_state)
					  (sha256 1 MOD_HASH))
     )

     (defun make_even (amt)
         (if (r (divmod amt 2))
	     (- amt 1)
	     amt))

     (defun-inline creator_amt (STATE ROYALTY)
     	 (if (f (r ROYALTY))
	     (f (divmod (* (f (r STATE)) (f (r ROYALTY))) 100))
	     0
	 )
     )



     ;; UPDATE STATE
     (defun update_state (MOD_HASH STATE ROYALTY my_amount new_state)
         (list
	       (list CREATE_COIN
	             (new_puzzle_hash MOD_HASH
		                      ROYALTY
				      new_state)
		     my_amount)

	       (list AGG_SIG_ME
	             (f (r (r (r STATE))))
		     (sha256tree new_state)))
     )

     ;; RECREATE WITH NEW STATE and PAYOUT
     (defun trade_coin (MOD_HASH
			STATE
			ROYALTY
			my_amount
			new_state
			payment_info
			)


         (list

	       (list CREATE_COIN			;; Recreate singleton
		     (new_puzzle_hash MOD_HASH
		     		      ROYALTY
				      new_state)
	             my_amount)

	       (list CREATE_COIN			;; Payout current owner
	             (f (r (r STATE)))
		     (make_even (- (f (r STATE))
		     	(creator_amt STATE
			             ROYALTY))))

	       (list CREATE_COIN			;; Payout Creator
	             (f ROYALTY)
		     (make_even (creator_amt STATE ROYALTY)))


	       (list CREATE_PUZZLE_ANNOUNCEMENT		;; Announce the p2 coin id
	             payment_info)

	       (list ASSERT_COIN_ANNOUNCEMENT		;; Assert the p2_coin spend
	             (sha256 payment_info
		             (sha256tree new_state)))

	       (list AGG_SIG_ME				;; Buyer signs
	             (f (r (r (r new_state))))
		     (sha256 MOD_HASH))
	 ))


    ;; ---------------------------------------------------------------------------
    ;; MAIN


    (if payment_info
	(if (f STATE)
	    (trade_coin MOD_HASH
                        STATE
			ROYALTY
		        (my_amount_truth Truths)
      		        new_state
		        payment_info
			)
	    (x "not for sale"))
        (update_state MOD_HASH
		      STATE
		      ROYALTY
		      (my_amount_truth Truths)
		      new_state
		      ))

)
